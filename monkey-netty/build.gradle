plugins {
    id 'java-library'
    id 'maven-publish'
}

ext {
    PUBLISH_GROUP_ID = group
    PUBLISH_VERSION = version
    PUBLISH_ARTIFACT_ID = name
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

dependencies {
    //Jme3
    api jmeGroup + ':jme3-core:' + jmeVersion
    //Netty.IO
    api group: 'io.netty', name: 'netty-all', version: nettyioVersion
    api group: 'io.netty', name: 'netty-transport', version: nettyioVersion
    // io.tlf.monkeynetty.test
    testImplementation jmeGroup + ':jme3-desktop:' + jmeVersion
    testImplementation jmeGroup + ':jme3-lwjgl3:' + jmeVersion
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'A implementation of a server-client communication system for jMonkeyEngine using Netty.IO that utilizes both TCP and UDP communication.'
                    url = 'https://github.com/tlf30/monkey-netty'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'tlf30'
                            name = 'Trevor Flynn'
                            email = 'trevorflynn@liquidcrystalstudios.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/tlf30/monkey-netty.git'
                        developerConnection = 'scm:git:ssh://tlf30/monkey-netty.git'
                        url = 'https://github.com/tlf30/monkey-netty/'
                    }
                }
            }
        }
    }
}
