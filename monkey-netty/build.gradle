plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/tlf30/monkey-netty")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            pom {
                name = 'Monkey Netty'
                description = 'A implementation of a server-client communication system for jMonkeyEngine using Netty.IO that utilizes both TCP and UDP communication.'
                url = 'https://github.com/tlf30/monkey-netty'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'tlf30'
                        name = 'Trevor Flynn'
                        email = 'liquidcrystalstudios@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/tlf30/monkey-netty.git'
                    developerConnection = 'scm:git:ssh://tlf30/monkey-netty.git'
                    url = 'https://github.com/tlf30/monkey-netty/'
                }
            }
        }
    }
}

dependencies {
    //Jme3
    api jmeGroup + ':jme3-core:' + jmeVersion
    //Netty.IO
    api group: 'io.netty', name: 'netty-all', version: nettyioVersion
    api group: 'io.netty', name: 'netty-transport', version: nettyioVersion
    // io.tlf.monkeynetty.test
    testImplementation jmeGroup + ':jme3-desktop:' + jmeVersion
    testImplementation jmeGroup + ':jme3-lwjgl3:' + jmeVersion
}





